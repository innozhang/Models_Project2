//
// 17-651: FSP model of a simple infusion pump
//

//======================
// Constants and Ranges
//======================

//
// States of the pump alarm
//
const AlarmActivated = 0    // Alarm currently active
const AlarmSilenced  = 1    // Alarm currently inactive

range AlarmStateT = AlarmActivated .. AlarmSilenced

//
// States of the pump settings
//
const ParamsNotSet = 2    // pump parameters not set yet
const ParamsSet    = 3    // pump parameters already set

range ParamsStateT = ParamsNotSet .. ParamsSet

//
// Locked/unlocked states of a line with respect to a pump channel
//
const LineUnlocked = 4  // line not locked into a pump channel 
const LineLocked   = 5  // line locked into a pump channel

range LineLockStateT = LineUnlocked .. LineLocked

//
// Locked/unlocked states of the pump unit
//
const UnitUnlocked = 6  // the keypad of the pump is not locked
const UnitLocked   = 7  // the keypad of the pump is locked

range UnitLockStateT = UnitUnlocked .. UnitLocked

//
// Model Power
//

POWER = UNPLUGGED,

UNPLUGGED = (plug_in->turn_on->RUNNING),
RUNNING = (unplug->POWER
		  |turn_off->turn_on->RUNNING).


//
// Model User Interface
//

USERINTERFACE = (turn_on->SETUP[ParamsNotSet][LineUnlocked]),

SETUP[params:ParamsStateT][lineLock:LineLockStateT] = 
(
    unplug -> USERINTERFACE
    |
    turn_off -> USERINTERFACE
    |
    when (params == ParamsNotSet && lineLock == LineUnlocked)
        set_rate -> enter_value ->
            (press_set -> SETUP[ParamsSet][lineLock]
             |
             press_cancel -> SETUP[ParamsNotSet][lineLock])
    |
    when (params == ParamsSet && lineLock == LineUnlocked)
        clear_rate -> SETUP[ParamsNotSet][lineLock]
    |
    when (params == ParamsSet && lineLock == LineUnlocked)
        connect_set -> purge_air -> lock_line -> SETUP[params][LineLocked]
    |
    when (lineLock == LineLocked)
        confirm_settings -> CONFIRMED
    |
    when (lineLock == LineLocked)
        erase_and_unlock_line -> SETUP[params][LineUnlocked]
),

CONFIRMED = (
	unplug -> USERINTERFACE
	|
	turn_off -> USERINTERFACE
	|
	change_settings -> SETUP[ParamsSet][LineLocked]).

//
//Pump in infusion mode:
//
LINE = (confirm_settings->INFUSION[UnitUnlocked]),
INFUSION[unitLock:UnitLockStateT] =
(
	unplug -> LINE
	|
	turn_off -> LINE
	|
	when (unitLock == UnitUnlocked)
		lock_unit -> INFUSION[UnitLocked]
	|
	when (unitLock == UnitLocked)
		change_settings->LINE
	|
	when (unitLock == UnitLocked)
		unlock_unit -> INFUSION[UnitUnlocked]
	|
	dispense_main_med_flow -> INFUSION[unitLock] // Normal operation
	|
	flow_blocked -> BLOCKED[unitLock]
),

BLOCKED[unitLock:UnitLockStateT]= (
	unplug -> LINE
	|
	turn_off -> LINE
	|
	flow_unblocked -> INFUSION[unitLock]
).

// 
// Model the alarm
//
ALARM = (flow_blocked -> sound_alarm ->INFUSION_BLOCKED[AlarmActivated]),
INFUSION_BLOCKED[alarm:AlarmStateT] = (
	unplug->ALARM
	|
	turn_off->ALARM
	|
	when (alarm == AlarmActivated)
		sound_alarm->INFUSION_BLOCKED[alarm]
	|
	when (alarm == AlarmActivated)
		silence_alarm->INFUSION_BLOCKED[AlarmSilenced]
	|
	flow_unblocked->ALARM
).


||PUMP = (POWER || USERINTERFACE || l[i:1..2]:LINE || l[i:1..2]::ALARM).



